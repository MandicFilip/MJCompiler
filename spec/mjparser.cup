package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:

	boolean errorDetected = false;

	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROGRAM, NEW, ENUM, RETURN;
terminal PRINT, READ, CONST, VOID, IF, ELSE, FOR, BREAK, CONTINUE;
terminal PLUS, MINUS, MUL, DIV, MOD, EQUAL, UNEQUAL, GREATER;
terminal GREATER_EQUAL, SMALLER, SMALLER_EQUAL, AND, OR, ASSIGNMENT;
terminal INC, DEC, SEMI, COMMA, DOT, LBRACE, RBRACE, LSQBRACE;
terminal RSQBRACE, LPAREN, RPAREN;
terminal String IDENT;
terminal String CHARCONST;
terminal Integer NUMBER;
terminal Boolean BOOL_CONST;

nonterminal AllDeclList, DeclType, ConstDeclExpression, ConstDeclList, ConstDecl, ConstKind;
nonterminal VarDeclExpression, VarsList, VarDecl, OptVar, ArrOpt;
nonterminal EnumDecl, EnumList, EnumMember, EnumInit;
nonterminal RetType, FormParams, FormPara, MethodDeclList;
nonterminal StatementList, Statement, OptElsePart, OptDesignatorStatement, OptCondition, ExpressionToPrint;
nonterminal DesignatorStatement, OptDesignatorStatementExtension, Condition;
nonterminal CondTerm, CondFact, SignTerm;
nonterminal ActualPars, ActualParamList, OptDesignatorPart, FieldOption, ArrayOption;
nonterminal AssignOP, RelOp, Addop, MulOp;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program, Designator, MethodDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Expr, Factor;

Program ::= (Program) PROGRAM IDENT:pName AllDeclList LBRACE MethodDeclList RBRACE;

AllDeclList ::= (ALLDeclarationsList) AllDeclList DeclType
              | (NoDeclaration)
              ;

DeclType ::= (ConstDeclaration) ConstDeclExpression
           | (VarDeclaration) VarDeclExpression
           | (EnumDeclaration) EnumDecl
           ;


Type ::= (Type) IDENT:tName;


ConstDeclExpression ::= (ConstDeclarationExpression) CONST Type:constType ConstDeclList SEMI;

ConstDeclList ::= (ConstDeclarationList) ConstDeclList COMMA ConstDecl
                  |
                  (SingleConstDeclaration) ConstDecl
                  ;

ConstDecl ::= (ConstDeclaration) IDENT:constName ASSIGNMENT ConstKind:constValue;

ConstKind ::= (ConstNumber) NUMBER:numValue
            |
            (ConstChar) CHARCONST:charValue
            |
            (ConstBool) BOOL_CONST:boolValue
            ;

VarDeclExpression ::= (VarDeclarationExpression) Type:varType VarsList SEMI;

VarsList ::= (VarsList) VarsList COMMA VarDecl
             |
             (SingleVarDeclaration) VarDecl;

VarDecl ::= (VarDeclaration) IDENT:varName ArrOpt;

ArrOpt ::= (ArrayOption) LSQBRACE RSQBRACE
            |
            (NoArrayOption);

EnumDecl ::= (EnumDeclaration) ENUM IDENT:eName LBRACE EnumList RBRACE;

EnumList ::= (EnumList) EnumList COMMA EnumMember
            | (SingleEnumMember) EnumMember
            ;

EnumMember ::= (EnumMember) IDENT:enumMemberName EnumInit;

EnumInit ::= (EnumInit) ASSIGNMENT NUMBER:enumMemberValue;

RetType ::= (RetVoid) VOID:retVoid | (RetType) Type:retType;

FormParams ::= (FormParamsDeclaration) FormParams COMMA FormPara
              | (SingleFormParametarDeclaration) FormPara
              | (NoFormParamsDeclaration)
              ;

FormPara ::= (FormalParams) Type:paramType IDENT:paramName ArrOpt;

MethodDeclList ::= (MethodDeclarationList) MethodDeclList MethodDecl
					|
					(NoMethodDecl)/* epsilon */
					;

MethodDecl ::= (MethodDecl) RetType IDENT:methodName LPAREN FormParams RPAREN OptVar LBRACE Statement RBRACE;

StatementList ::= (StatementList) StatementList Statement
                    |
                  (NoStatementList)
                    ;

Statement ::= (DesignatorStatement) DesignatorStatement SEMI
			  |
			  (IfStatement) IF LPAREN Condition RPAREN Statement OptElsePart
			  |
			  (ForStatement) FOR LPAREN OptDesignatorStatement SEMI OptCondition SEMI OptDesignatorStatement RPAREN Statement
			  |
			  (BreakStatement) BREAK SEMI
			  |
			  (ContinueStatement) CONTINUE SEMI
			  |
			  (ReadStatement) READ LPAREN Designator:dest RPAREN SEMI
			  |
			  (PrintStatement) PRINT LPAREN  ExpressionToPrint RPAREN SEMI
			  |
			  (ReturnExpStatement) RETURN Expr:retExp SEMI
			  |
			  (ReturnNopStatement) RETURN SEMI
			  |
			  (StatementBlock) LBRACE Statement RBRACE
			  ;

OptElsePart ::= (ElseOption) ELSE Statement
                |
                (NoElseOption)
                ;

OptDesignatorStatement ::= (DesignatorStatement) DesignatorStatement
                           |
                           (NoDesignatorStatement)
                           ;

OptCondition::= (Condition) Condition
                |
                (NoCondition)
                ;

ExpressionToPrint ::= (ExpressionToPrintList) ExpressionToPrint COMMA NUMBER:value
                      |
                      (SingleExpressionToPrint) Expr:exprToPrint
                      ;


DesignatorStatement ::= (DesignatorStatement) Designator:dest OptDesignatorStatementExtension;

OptDesignatorStatementExtension ::= (AssignExtension) AssignOP Expr:expValue
                                    |
                                    (ParametarsExtension) LPAREN ActualPars RPAREN
                                    |
                                    (IncExtension) INC
                                    |
                                    (DecExtension) DEC
                                    |
                                    (NoExtension)
                                    ;

Condition ::= (TermListCondition) Condition OR CondTerm
              |
              (SingleTermCondition) CondTerm
              ;

CondTerm ::= (CondFactListTerm) CondFact AND CondFact
             |
             (SingleCondFactTerm) CondFact;

CondFact ::= (ConditionFactor) CondFact RelOp Expr
                | (SingleCondFact) Expr
                ;

Expr ::= (TermListExpr) Expr Addop Term
          |
          (SingleTermExpr) SignTerm;

SignTerm ::= (MinusTerm) MINUS Term
             |
             (NoSignTerm) Term;

Term ::= (FactorListTerm) Term MulOp Factor
         |
         (SingleFactorTerm) Factor;

Factor ::= (NumberFactor) NUMBER:valueNumber
		   |
		   (CharFactor) CHARCONST:valueChar
		   |
		   (BoolFactor) BOOL_CONST:valBool
		   |
		   (NewTypeFactor) NEW Type ArrayOption
		   |
		   (DesignatorFactor) Designator:d
		   |
		   (FunCallFactor) Designator:func LPAREN ActualPars RPAREN
		   |
		   (ParenFactor) LPAREN Expr RPAREN
		   ;

ActualPars ::= (ActualParametarsList) ActualParamList
			   |
			   (NoActualParametarsList)
			   ;

ActualParamList ::= (ParametarsList) ActualParamList COMMA Expr
					 |
					 (SingleExpr) Expr
					 ;

Designator ::= (Designator) IDENT:dest OptDesignatorPart;

OptDesignatorPart ::= (OptionalFieldDesignatorPart) FieldOption
                    | (OptionalArrayDesignatorPart) ArrayOption
                    | (NoOptionalPart)
                    ;

FieldOption ::= (Field) DOT IDENT:desField;

ArrayOption ::= (Array) LSQBRACE Expr:arrExp RSQBRACE;

AssignOP ::= (Assignment) ASSIGNMENT;

RelOp ::= (Equal) EQUAL
        | (Unequal) UNEQUAL
        | (Greater) GREATER
        | (GreaterEqual) GREATER_EQUAL
        | (Smaller)SMALLER
        | (SmallerEqual) SMALLER_EQUAL;

Addop ::= (PlusOp) PLUS | (MinusOp) MINUS;

MulOp ::= (MulOp) MUL | (DivOp) DIV | (ModOp) MOD;
